# #!/usr/bin/env zsh

# zmodload zsh/zprof

{{- if eq .chezmoi.os "linux" }}
if [[ ! $DISPLAY && $XDG_VTNR -eq 1 ]]; then
    exec startx
fi
{{- end }}

fn=${HOME}/.local/bin
fpath=($HOME/.zsh/completion /usr/local/share/zsh/site-functions $fn $fpath)

for function in $fn/*; do
    autoload -Uz ${function:t}
done

# extra files in ~/.zsh/configs/pre , ~/.zsh/configs , and ~/.zsh/configs/post
# these are loaded first, second, and third, respectively.
_load_settings() {
    _dir="$1"
    if [ -d "$_dir" ]; then
        if [ -d "$_dir/pre" ]; then
            for config in "$_dir"/pre/**/*~*.zwc(N-.); do
                . $config
            done
        fi

        for config in "$_dir"/**/*(N-.); do
            case "$config" in
                "$_dir"/(pre|post)/*|*.zwc)
                    :
                    ;;
                *)
                    . $config
                    ;;
            esac
        done

        if [ -d "$_dir/post" ]; then
            for config in "$_dir"/post/**/*~*.zwc(N-.); do
                . $config
            done
        fi
    fi
}

_load_settings "$HOME/.zsh"

# run ssh-agents
if [ $(ps ax | grep ssh-agent | wc -l) -gt 0 ] ; then
    # echo "ssh-agent is already running"
else
    eval `ssh-agent -s`
    if [ "$(ssh-add -l)" == "The agent has no identities." ] ; then
        ssh-add -t 4h
    fi

    # Don't leave extra agents around: kill it on exit. You may not want this part.
    trap "ssh-agent -k" exit
fi

# aliases
[[ -f ~/.aliases ]] && source ~/.aliases

# .localrc contains all the local dev variables
if [[ -f ~/.localrc ]]; then
    source ~/.localrc
fi

typeset -U path cdpath fpath manpath

# zprof
